# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20210202
pkgrel=1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='https://git.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('bash' 'openssh' 'subversion' 'rsync' 'arch-install-scripts'
         'git' 'bzr' 'mercurial' 'diffutils' 'util-linux')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
source=(https://sources.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz{,.sig}
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-loggin.patch'
        '0006-archbuild-no-setarch.patch'
        '0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch'
        '0008-Revert-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACK.patch'
        '0009-makechrootpkg-don-t-delete-PACKAGER.patch')

validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53'
              '4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC'
              '86CFFCA918CF3AF47147588051E8B148A9999C34'
              '8FC15A064950A99DD1BD14DD39E4B877E62EB915'
              '8218F88849AAC522E94CF470A5E9288C4FA415FA'
              'B81B051F2D7FC867AAFF35A58DBD63B82072D77A'
              'F3691687D867B81B51CE07D9BBE43771487328A9'
              '6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'
              'E240B57E2C4630BA768E2F26FC1B547C8D8172C8')
sha256sums=('ad1a65f0f349093b557bd6f45fd5f84ba2be246702908193ae7351457a218d6e'
            'SKIP'
            '72cb5a158fbf75a87d945b43a32a7c1c7d0bff2eea908b87326b1cff9e845dac'
            '601c2b0d30649e041231cdaf481133c55ff41e5ececaf79c17fdad7880cd5493'
            '483adfb3369df9f6ea6dae5b0d4e9e08e9147c6c41095c3816648f62fb68b50c'
            'd069ecc26a1c0e698d3f562acd38bc0729545a5bed80a661a6d60509d1c0143a'
            '11a629faae472215e4400aee72c22380d838b01962427eee92af384a27515c1d'
            '35cba36503ae76ffbcb1902680e57d491dcb7531263f95f5172e681933393342'
            '98a741e0812cc1ede7ec65425959cd41c3770f3d4b5e809ede9691cc4634bc6e'
            '52fc1a462bf7821e251cfec02dc047640c8ea960cc48decd13185f427593ca88'
            'f45e06c8cf7017aa937cd1ada643453a02106c41744b431579bb4010cba63b76')

prepare() {
  cd "${_pkgname}-${pkgver}"
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = 0*.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd ${_pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${_pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
